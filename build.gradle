apply plugin: 'com.android.library'
apply plugin: 'maven'

def publishGroupId = 'io.scanbot'
def publishArtifactId = 'tess-two'
def publishVersionName = "3.0.0-SNAPSHOT"

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion buildToolsVersion

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName publishVersionName
        consumerProguardFiles 'proguard-rules.pro'

        externalNativeBuild {
            ndkBuild {
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
                arguments '-j16'
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }
    packagingOptions {
        exclude '**/libc++_shared.so'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    api 'androidx.annotation:annotation:1.0.0'
}

uploadArchives {
    repositories {
        mavenDeployer {
            snapshotRepository(url: "http://nexus.scanbot.io/nexus/content/repositories/snapshots/") {
                authentication(userName: project.getProperty('NEXUS_LOGIN'), password: project.getProperty('NEXUS_PASSWORD'))
            }
            repository(url: "http://nexus.scanbot.io/nexus/content/repositories/releases/") {
                authentication(userName: project.getProperty('NEXUS_LOGIN'), password: project.getProperty('NEXUS_PASSWORD'))
            }

            pom.version = publishVersionName
            pom.artifactId = publishArtifactId
            pom.groupId = publishGroupId
        }
    }
}
